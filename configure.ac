# AC_INIT (package, version, [bug-report], [tarname])
AC_INIT(DDD,1.4.1,[Yann.Thierry-Mieg@lip6.fr])

# We use automake to build makefiles
AM_INIT_AUTOMAKE(-Wall)

# programs used to build
# we make static libs (use ranlib)
AC_PROG_RANLIB

# we compile C++
# set Cxx compilation flags to either the value passed in
# ./configure --cxxflags=
# or none to change behavior from default cxx flags generated by autoconf
# default : -02 -g
#CXXFLAGS=${CXXFLAGS-} 
test -z "$CFLAGS" && CFLAGS=
test -z "$CXXFLAGS" && CXXFLAGS=

#compile in C++
AC_PROG_CXX

reentrant=false
parallel=false

# option to indicate libtbb folder
AC_ARG_WITH([libtbbinc],
            [AC_HELP_STRING([--with-libtbbinc=/include/path/libtbb],
                            [specify the include path of libtbb])])

AC_ARG_WITH([libtbbbin],
            [AC_HELP_STRING([--with-libtbbbin=/path/to/libtbb.a],
                            [specify the library linker path of libtbb])])



AC_ARG_ENABLE(	[reentrant],
        [AC_HELP_STRING([--enable-reentrant],[turn on on thread-safe mode])],
        [  case "${enable_reentrant}" in
            yes) reentrant=true
                ;;
            no) 
                ;;
            *)  AC_MSG_ERROR(Bad value ${enableval})
                ;;
            esac])


AC_ARG_ENABLE(	[parallel],
        [AC_HELP_STRING([--enable-parallel],[turn on on parallel mode])],
        [  case "${enable_parallel}" in
            yes) parallel=true
                 reentrant=true
                ;;
            no) 
                ;;
            *)  AC_MSG_ERROR(Bad value ${enableval})
                ;;
            esac])

if test "x$parallel" = "xtrue"; then
    AC_DEFINE([PARALLEL_DD],1,[Define if you want parallel code.])
fi

if test "x$reentrant" = "xtrue"; then
    AC_DEFINE([REENTRANT],1,[Define if you want a thread-safe library.])
fi

AM_CONDITIONAL( [REENTRANT], [test "x$reentrant" = "xtrue" ])

AM_CONDITIONAL([WITH_LIBTBBINC_PATH], [test "x${with_libtbbinc}" != x])
if test "x${with_libtbbinc}" != x; then
    AC_SUBST([LIBTBB_INC],["${with_libtbbinc}"])
fi

AM_CONDITIONAL([WITH_LIBTBBBIN_PATH], [test "x${with_libtbbbin}" != x])
if test "x${with_libtbbbin}" != x; then
    AC_SUBST([LIBTBB_BIN],["${with_libtbbbin}"])
fi

AC_CONFIG_FILES([   Makefile
                    demo/Makefile
                    demo/hanoi/Makefile
                    demo/expr/Makefile
                    demo/morpion/Makefile
                    src/Makefile])
                    # doc/Doxyfile
                    # doc/Makefile])

# Do it !! (mandatory)
AC_OUTPUT
